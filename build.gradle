apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.7
version = '1.0.0'

project.group = 'com.adjectivecolournoun'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5"
    }
}

repositories {
    jcenter()
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:$groovyVersion"

    compile 'org.slf4j:slf4j-api:1.7.7'

    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        transitive = false
    }
    testCompile 'junit:junit:4.11'
}

groovydoc {
    groovyClasspath = configurations.compile
    link 'https://docs.oracle.com/javase/7/docs/api/', 'java.lang.', 'java.util.'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.groovy
    classifier = 'sources'
}

task groovydocJar(type: Jar, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    classifier = 'javadoc'
}

task exportJavadoc(type: Copy, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    into 'javadoc'
}

artifacts {
    archives jar
    archives sourcesJar
    archives groovydocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact groovydocJar {
                classifier 'javadoc'
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey

    dryRun = false
    publish = false

    publications = ['mavenJava']

    pkg {
        repo = 'maven'
        name = 'throughputcounter'
        desc = 'Useful throughput counters'

        websiteUrl = 'https://github.com/andyjduncan/throughputcounter'
        vcsUrl = 'https://github.com/andyjduncan/throughputcounter.git'
        issueTrackerUrl = 'https://github.com/andyjduncan/throughputcounter/issues'

        licenses = ['Apache-2.0']

        version {
            vcsTag = project.version
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}